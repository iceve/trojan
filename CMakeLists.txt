cmake_minimum_required(VERSION 2.8.12)
project(Trojan)
option(STATIC "Link libraries statically")
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++11 -O3 -Wall -Wextra")
set(THREADS_PREFER_PTHREAD_FLAG ON)
if(STATIC)
    set(Boost_USE_STATIC_LIBS ON)
endif()
add_executable(trojan src/clientsession.cpp src/config.cpp src/log.cpp src/main.cpp src/serversession.cpp src/service.cpp src/session.cpp src/socks5address.cpp src/ssldefaults.cpp src/trojanrequest.cpp src/udppacket.cpp src/version.cpp)
find_package(Threads REQUIRED)
target_link_libraries(trojan ${CMAKE_THREAD_LIBS_INIT})
find_package(Boost 1.54.0 REQUIRED COMPONENTS system)
include_directories(${Boost_INCLUDE_DIR})
target_link_libraries(trojan ${Boost_LIBRARIES})
find_package(OpenSSL 1.0.2 REQUIRED)
include_directories(${OPENSSL_INCLUDE_DIR})
target_link_libraries(trojan ${OPENSSL_LIBRARIES})
if(WIN32)
    target_link_libraries(trojan wsock32 ws2_32 crypt32)
    if(STATIC)
        target_link_libraries(trojan -static)
    endif()
else()
    set(SYSTEMD_SERVICE AUTO CACHE STRING "Install systemd service")
    set_property(CACHE SYSTEMD_SERVICE PROPERTY STRINGS AUTO ON OFF)
    set(SYSTEMD_SERVICE_PATH /usr/lib/systemd/system CACHE PATH "Systemd service path")
    include(GNUInstallDirs)
    install(TARGETS trojan DESTINATION ${CMAKE_INSTALL_BINDIR})
    install(FILES examples/client.json-example examples/server.json-example DESTINATION ${CMAKE_INSTALL_DATAROOTDIR}/trojan)
    install(FILES examples/server.json-example DESTINATION ${CMAKE_INSTALL_FULL_SYSCONFDIR} RENAME trojan.json)
    if(SYSTEMD_SERVICE STREQUAL AUTO)
        if(EXISTS /usr/lib/systemd/system)
            set(SYSTEMD_SERVICE ON)
        endif()
        if(EXISTS /lib/systemd/system)
            set(SYSTEMD_SERVICE ON)
            set(SYSTEMD_SERVICE_PATH /lib/systemd/system CACHE PATH "Systemd service path" FORCE)
        endif()
    endif()
    if(SYSTEMD_SERVICE STREQUAL ON)
        configure_file(examples/trojan.service-example trojan.service)
        install(FILES ${CMAKE_BINARY_DIR}/trojan.service DESTINATION ${SYSTEMD_SERVICE_PATH})
    endif()
    enable_testing()
    add_test(NAME LinuxSmokeTest-basic
             COMMAND sh ${CMAKE_SOURCE_DIR}/tests/LinuxSmokeTest/basic.sh ${CMAKE_BINARY_DIR}/trojan
             WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}/tests/LinuxSmokeTest)
    add_test(NAME LinuxSmokeTest-fake-client
             COMMAND sh ${CMAKE_SOURCE_DIR}/tests/LinuxSmokeTest/fake-client.sh ${CMAKE_BINARY_DIR}/trojan
             WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}/tests/LinuxSmokeTest)
endif()
